entrypoints Defs, Expr;

comment "--";
comment "{-" "-}";

layout toplevel;

Defs.      Defs    ::= [Def];
DefType.   Def     ::= PIdent ":" Expr;
DefFun.    Def     ::= Pattern "=" Expr;
Pattern.   Pattern ::= PIdent [ParPat];
ParVar.    ParPat  ::= PIdent;
ParPat.    ParPat  ::= PPar Pattern ")";

terminator ParPat "";
separator  Def ";";

Lam.      Expr  ::= PLam [Arg] "->" Expr;
Arr.      Expr1 ::= Expr2 "->" Expr1;
Pi.       Expr1 ::= [TypedVar] "->" Expr1;
App.      Expr2 ::= Expr2 Expr3;
Var.      Expr3 ::= Arg;
Universe. Expr3 ::= U;
_.        Expr  ::= Expr1;
_.        Expr1 ::= Expr2;
_.        Expr2 ::= Expr3;
Paren.    Expr3 ::= PPar Expr ")";

Arg.       Arg ::= PIdent;
NoArg.     Arg ::= Pus;
separator nonempty Arg "";

TypedVar. TypedVar ::= PPar Expr ":" Expr ")";
terminator nonempty TypedVar "";

position token U ('T' 'y' 'p' 'e' digit*);
position token PLam '\\';
position token PPar '(';
position token Pus '_';
position token PIdent (letter(letter|digit|'\''|'_'|'-')*);
