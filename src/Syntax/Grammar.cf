entrypoints Defs, Expr;

comment "--";
comment "{-" "-}";

layout toplevel;

Defs.         Defs     ::= [Def];
DefType.      Def      ::= PIdent ":" Expr;
DefFun.       Def      ::= Pattern "=" Expr;
DefFunEmpty.  Def      ::= Pattern;
DefData.      Def      ::= "data" PIdent [DataTele] "=" [Con];
DefDataEmpty. Def      ::= "data" PIdent [DataTele];
separator     Def      ";";

Pattern.   Pattern  ::= PIdent [ParPat];
ParVar.    ParPat   ::= Arg;
ParEmpty.  ParPat   ::= PPar ")";
ParPat.    ParPat   ::= PPar Pattern ")";
terminator ParPat   "";

Con.       Con      ::= PIdent [ConTele];
separator  nonempty Con "|";

VarTele.   ConTele  ::= PPar Expr ":" Expr ")";
TypeTele.  ConTele  ::= Expr3;
terminator ConTele  "";

DataTele.  DataTele ::= PPar Expr ":" Expr ")";
terminator DataTele "";

PiTele.    PiTele   ::= PPar Expr ":" Expr ")";
terminator nonempty PiTele "";

Lam.       Expr     ::= PLam [Arg] "->" Expr;
Arr.       Expr1    ::= Expr2 "->" Expr1;
Pi.        Expr1    ::= [PiTele] "->" Expr1;
App.       Expr2    ::= Expr2 Expr3;
Var.       Expr3    ::= Arg;
Universe.  Expr3    ::= U;
_.         Expr     ::= Expr1;
_.         Expr1    ::= Expr2;
_.         Expr2    ::= Expr3;
Paren.     Expr3    ::= PPar Expr ")";

Arg.       Arg      ::= PIdent;
NoArg.     Arg      ::= Pus;
separator  nonempty Arg "";

position token U ('T' 'y' 'p' 'e' digit*);
position token PLam '\\';
position token PPar '(';
position token Pus '_';
position token PIdent (letter(letter|digit|'\''|'_'|'-')*);
